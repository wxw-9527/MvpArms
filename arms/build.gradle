plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
    id 'kotlin-parcelize'
    id 'com.google.dagger.hilt.android' // Hilt
    id 'com.huawei.agconnect' // 华为
    id 'maven-publish' // fix jitpack warning：'Gradle 'publishToMavenLocal' task not found. Please add the 'maven-publish' or 'maven' plugin.'
}

android {
    namespace 'com.rouxinpai.arms'
    compileSdk 34
    defaultConfig {
        minSdk 21
        targetSdk 34
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlin {
        jvmToolchain(17)
    }
    buildFeatures {
        viewBinding true
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    //
    api 'androidx.appcompat:appcompat:1.6.1'
    api 'com.google.android.material:material:1.11.0'
    // 官方扩展程序
    api 'androidx.core:core-ktx:1.13.1'
    api 'androidx.activity:activity-ktx:1.9.0'
    api 'androidx.fragment:fragment-ktx:1.6.2'
    api 'androidx.work:work-runtime-ktx:2.8.1'
    // 官方控件
    api 'androidx.constraintlayout:constraintlayout:2.1.4'
    api 'androidx.recyclerview:recyclerview:1.3.2'
    api 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    // 依赖注入：https://dagger.dev/hilt/
    implementation 'com.google.dagger:hilt-android:2.51.1'
    ksp 'com.google.dagger:hilt-compiler:2.51.1'
    // RxJava
    api 'io.reactivex.rxjava3:rxandroid:3.0.2'
    api 'io.reactivex.rxjava3:rxjava:3.1.8'
    api 'com.jakewharton.rxbinding4:rxbinding:4.0.0'
    // 网络请求
    api 'com.squareup.okhttp3:okhttp:4.12.0'
    api 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    api 'com.squareup.retrofit2:retrofit:2.11.0'
    api 'com.squareup.retrofit2:converter-gson:2.11.0'
    api 'com.squareup.retrofit2:adapter-rxjava3:2.11.0'
    api 'com.google.code.gson:gson:2.11.0'
    // 图片缓存
    api 'com.github.bumptech.glide:glide:4.16.0'
    ksp 'com.github.bumptech.glide:compiler:4.16.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.16.0'
    // 基于 mmap 的高性能通用 key-value 组件
    api 'com.tencent:mmkv:1.3.4'
    // ObjectBox
    debugImplementation 'io.objectbox:objectbox-android-objectbrowser:3.8.0'
    releaseImplementation 'io.objectbox:objectbox-android:3.8.0'
    // 通信
    api 'org.greenrobot:eventbus:3.3.1'
    // 日志打印
    api 'com.jakewharton.timber:timber:5.0.1'
    // 权限
    api 'com.github.getActivity:XXPermissions:18.63'
    // RecyclerView相关
    api 'io.github.cymchad:BaseRecyclerViewAdapterHelper4:4.1.4'
    api 'com.github.fondesa:recycler-view-divider:3.6.0'
    // 气泡提示
    implementation 'com.github.skydoves:balloon:1.6.4'
    // 下拉框
    api 'com.github.skydoves:powerspinner:1.2.7'
    // 工具类
    api 'com.blankj:utilcodex:1.31.1'
    // 图片选择框架
    api 'io.github.lucksiege:pictureselector:v3.11.2'
    api 'io.github.lucksiege:compress:v3.11.2'
    // 对话框相关
    api 'com.github.kongzue.DialogX:DialogX:0.0.50.beta14'
    api 'com.github.kongzue.DialogXSample:DatePicker:0.0.14'
    api 'com.github.kongzue.DialogXSample:CustomWheelPicker:0.0.14'
    // 缺省页
    api 'com.github.wxw-9527:MultiStatePage:1.0.3'
    // 步进器
    api 'com.github.wxw-9527:Stepper:1.1.4'
    // 图片查看库
    implementation 'io.github.FlyJingFish.OpenImage:OpenImageGlideLib:2.2.1'
    // 蓝牙打印机
    implementation files('libs/HCCTG_printersdkv5.7.0.jar') // 华晨联创
    implementation files('libs/HPRT_CPCL_SDK_V1.22.01.jar') // 汉印
    implementation files('libs/HPRT_lzo_V1.0.jar') // 汉印
    implementation files('libs/IBOWLDER_fat-generic-cpcl-bluetooth-classic-0.1.16-GA.jar') // 鹏研科技
    implementation files('libs/IBOWLDER_fat-psdk-patch-0.1.16-GA.jar') // 鹏研科技
    // 华为
    implementation 'com.huawei.agconnect:agconnect-core:1.9.1.301'
    implementation 'com.huawei.agconnect:agconnect-crash:1.9.1.301'
    implementation 'com.huawei.agconnect:agconnect-crash-native:1.9.1.301' // 崩溃NDK
    implementation 'com.huawei.agconnect:agconnect-apms:1.6.2.300' // 性能管理SDK
    implementation 'com.huawei.hms:hianalytics:6.12.0.300' // 华为分析SDK
    //
    api 'com.github.bingoogolapple.BGABadgeView-Android:api:1.2.0'
}

apply plugin: 'io.objectbox' // ObjectBox

// fix jitpack warning：'Gradle 'publishToMavenLocal' task not found. Please add the 'maven-publish' or 'maven' plugin.'
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.wxw-9527'
                artifactId = 'MvpArms'
                version = '2.9.3.01'
            }
        }
    }
}